
open Printf

type t:
  inf : Inf.t
  dummy : unit

var initial_buffer_size = 256
var basic_offset = 2

def create(): {
  inf = Inf.create();
  dummy = ();
}

def with_open_in(fname_in, func):
  var chan_in = open_in(fname_in)
  try:
    var x = func(chan_in)
    close_in(chan_in)
    x
  with:
    case exn:
      close_in_noerr(chan_in)
      raise(exn)

def with_open_out(fname_out, func):
  var chan_out = open_out(fname_out)
  try:
    var x = func(chan_out)
    close_out(chan_out)
    x
  with:
    case exn:
      close_out_noerr(chan_out)
      raise(exn)

def compile_file(compiler, fname_in, fname_out):
  with_open_in(fname_in)^(chan_in):
  with_open_out(fname_out)^(chan_out):
    var strm = Stream.of_channel(chan_in)
    var src = Source.create(fname_in, strm)
    var lexer = Lexer.create(src)
    var parser = Parser.create(lexer)
    var trans = Trans.create(basic_offset)
    try:
      def loop():
        match Parser.parse(parser):
          case None:
            true
          case Some(top):
            var result = Trans.translate_top(trans, top)
            fprintf(chan_out, "%s\n", result)
            loop()
      loop()
    with:
      case Failure(message):
        eprintf("%s", message)
        flush(stderr)
        false

def compile_string(compiler, fname, str):
  var decls = Buffer.create(initial_buffer_size)
  var output = Buffer.create(initial_buffer_size)
  var strm = Stream.of_string(str)
  var src = Source.create(fname, strm)
  var lexer = Lexer.create(src)
  var parser = Parser.create(lexer)
  var trans = Trans.create(basic_offset)
  try:
    def loop(compiler):
      match Parser.parse(parser):
        case None:
          Some(compiler, Buffer.contents(decls), Buffer.contents(output))
        case Some(top):
          var (inf, decl) = Inf.infer_top(compiler.inf, top)
          Buffer.add_string(decls, Decl.show(decl))
          var result = Trans.translate_top(trans, top)
          Buffer.add_string(output, result)
          loop(compiler.{inf = inf})
    loop(compiler)
  with:
    case Failure(message):
      eprintf("%s", message)
      flush(stderr)
      None

def compile(compiler, fnames):
  def comp(fname_in):
    var fname_out = if Filename.check_suffix(fname_in, ".yz"):
      sprintf("%s.ml", Filename.chop_suffix(fname_in, ".yz"))
    else:
      sprintf("%s.ml", fname_in)
    compile_file(compiler, fname_in, fname_out)    
  List.for_all(comp, fnames)

def read(buf):
  printf("> ")
  var line = read_line()
  if line = "":
    Buffer.contents(buf)
  else:
    Buffer.add_string(buf, sprintf("%s\n", line))
    read(buf)

def interactive(compiler):
  try:
    var str = read(Buffer.create(initial_buffer_size))
    match compile_string(compiler, "<interactive>", str):
      case None:
        interactive(compiler)
      case Some(compiler, decls, output):
        printf("decls:\n%s", decls)
        printf("output:\n%s", output)
        interactive(compiler)
  with:
    case End_of_file:
      ()
